buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://download.crashlytics.com/maven' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.0'
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.+'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'crashlytics'
apply plugin: 'android-apt'
apply plugin: 'robolectric'

repositories {
    maven { url "http://dl.bintray.com/populov/maven" }
    mavenCentral()
    maven { url 'http://download.crashlytics.com/maven' }
    maven { url "https://github.com/dahlgren/pulltorefresh-aar/raw/master" }
    maven { url 'http://clinker.47deg.com/nexus/content/groups/public' }
    maven { url 'https://github.com/TradeHero/TH_MAVEN/raw/master' }
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.1.0"

    defaultConfig {
        applicationId "com.tradehero.th"
        minSdkVersion 14
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.dev'
            versionNameSuffix '-dev'
        }
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard/proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        chinese {
            applicationIdSuffix '.cn'
            versionNameSuffix '-cn'
        }
        googleplay {
            versionNameSuffix '-gp'
        }
    }

    sourceSets {
        main {
            manifest {
                srcFile 'AndroidManifest.xml'
            }
            java {
                srcDir 'src/main/java'
            }
            aidl {
                srcDir 'src/main/java'
            }
            res {
                srcDir 'res'
            }
            assets {
                srcDir 'assets'
            }
        }

        chinese {
            java {
                srcDir 'src/chinese/java'
            }
        }

        release {
            java {
                srcDir 'src/googleplay/java'
            }
        }

        debug {
            java {
                srcDir 'src/googleplay/java'
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    chineseCompile fileTree(dir: 'libs/baidu', include: ['*.jar'])

    compile project(':ResideMenu')
    compile project(':samsung_iap')
    compile project(':facebook-android-sdk:facebook')

    compile ('com.actionbarsherlock:actionbarsherlock:4.4.0@aar') {
        exclude group: 'com.google.android', module: 'support-v4'
    }
    compile 'com.android.support:support-v4:19.1.0'
    compile ('com.github.chrisbanes.pulltorefresh:library:2.1.2-SNAPSHOT@aar') {
        // exclude group: 'com.android.support', module: 'appcompat-v7'
    }
    compile ('com.viewpagerindicator:library:2.4.1@aar') {
        exclude group: 'com.google.android', module: 'support-v4'
    }
    compile ('se.emilsjolander:stickylistheaders:2.4.1@aar') {
        exclude group: 'com.google.android', module: 'support-v4'
    }
    compile ('com.fortysevendeg.swipelistview:swipelistview:1.0-SNAPSHOT@aar') {
        // exclude group: 'com.android.support', module: 'appcompat-v7'
    }

    compile 'com.squareup.picasso:picasso:2.3.2'
    compile 'com.squareup.okhttp:okhttp:1.6.0'
    compile ('com.squareup.retrofit:retrofit:1.6.1') {
        exclude group: 'com.google.code.gson', module: 'gson'
    }
    compile ('com.squareup.retrofit:converter-simplexml:1.4.1') {
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'xpp3', module: 'xpp3'
    }
    compile 'com.squareup.dagger:dagger:1.2.1'
    provided 'com.squareup.dagger:dagger-compiler:1.2.1'

    compile 'com.jakewharton.timber:timber:2.2.2'
    compile 'com.urbanairship:urbanairship-lib:3.2.1'

    compile 'com.fasterxml.jackson.core:jackson-databind:2.4.0'
    compile 'com.jakewharton:disklrucache:2.0.2'

    compile 'com.tradehero.route:route:0.2-SNAPSHOT'

    compile 'org.jetbrains:annotations:13.0'
    compile 'com.crashlytics.android:crashlytics:1.1.10+'

    compile ('oauth.signpost:signpost-core:1.2.1.2') {
        exclude group: 'commons-codec', module: 'commons-codec'
    }
    compile ('oauth.signpost:signpost-commonshttp4:1.2.1.2') {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'commons-codec', module: 'commons-codec'
        exclude group: 'org.apache.httpcomponents', module: 'httpcore'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    compile 'org.twitter4j:twitter4j-core:3.0+'

    compile ('com.localytics.android:library:2.19.0') {
        exclude group: 'com.google.android', module: 'support-v4'
    }

    compile 'org.ocpsoft.prettytime:prettytime:3.2.1.Final'
    compile 'com.jakewharton:butterknife:4.0.1'

    // For Test
    androidTestCompile 'junit:junit:4.10'
    androidTestCompile 'org.robolectric:robolectric:2.3'
    androidTestCompile 'org.easytesting:fest-assert-core:2.0M10'
    androidTestCompile 'org.mockito:mockito-core:1.9.5'

    androidTestCompile 'org.apache.commons:commons-lang3:3.1'
    androidTestCompile 'org.apache.commons:commons-io:1.3.2'
}

File propFile = file('signing.properties');
if (propFile.exists()) {
    def Properties props = new Properties()
    props.load(new FileInputStream(propFile))

    if (props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        android.buildTypes.release.signingConfig = null
    }
} else {
    android.buildTypes.release.signingConfig = null
}